use Libraries.Game.Game
use Libraries.Game.Graphics.Drawable

class Main is Game
    GameRoom roo
    GameKeyboard GameKeyboardListener
    Clickable GameMouseListener
    GameCollision GameCollisionListener

    action Main
        GameKeyboardListener:roo = roo
        GameMouseListener:roo = roo
        GameCollisionListener:roo = roo
        // Fixed screensize / gamename
        SetScreenSize(roo:pg_ScreenSizeX , roo:pg_ScreenSizeY)
        SetGameName("Castle Crusher")
        StartGame()
    end

    action CreateGame
        EnablePhysics2D(true)
        SetGravity2D(0, -200)
        AddKeyboardListener(GameKeyboardListener)
        AddCollisionListener(GameCollisionListener)
        AddMouseListener(GameMouseListener)
        roo:LoadRoom("./start_menu.txt")
    end

    action Update(number seconds)
        // Check if there are objs that need to be removed
        repeat until roo:GetDelQueueSize() = 0
            Drawable tmpDraw = roo:DequeueDelObj()

            // Removing example cursor object
            if tmpDraw:GetName() = "objGameMouseFollower"
                Remove(roo:g_Cursors:RemoveFromFront())
            end

            Remove(tmpDraw)
        end

        // Check if there are objs that need to be added
        repeat until roo:GetAddQueueSize() = 0
            Drawable tmpDraw = roo:DequeueAddObj()

            // Adding example cursor object
            if tmpDraw:GetName() = "objGameMouseFollower"
                AddMouseMovementListener(roo:g_Cursors:GetFromFront())
                Add(roo:g_Cursors:GetFromFront())
            else
                Add(tmpDraw)        // Add standard Drawable
            end
        end
    end
end