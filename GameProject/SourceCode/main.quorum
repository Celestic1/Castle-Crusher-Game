use Libraries.Game.Game
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Events.CollisionListener2D
use Libraries.Interface.Events.CollisionEvent2D
use Libraries.Interface.Events.KeyboardListener
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Interface.Events.MouseListener
use Libraries.Interface.Events.MouseEvent
use Libraries.Compute.Vector2

class Main is Game, KeyboardListener, CollisionListener2D, MouseListener
    GameRoom roo

    g_CatIndex = -1
    Vector2 g_Jump
    Vector2 g_Left
    Vector2 g_Right

    // Initial Game Settings
    g_GameName = "Physics [cat] Game"
    g_ScreenSizeX = 1000
    g_ScreenSizeY = 320 
    g_GridX = 40          // This is the width  of each 'grid' on map
    g_GridY = 40          // This is the height of each 'grid' on map

    action Main
        SetScreenSize(g_ScreenSizeX , g_ScreenSizeY)    // Screen Size is fixed
        SetGameName(g_GameName)                         // Game Title is fixed
        StartGame()
    end

    action CreateGame
        EnablePhysics2D(true)
        SetGravity2D(0, -200)
        AddKeyboardListener(me)
        AddCollisionListener(me)
        AddMouseListener(me)
        g_Jump:Set(0, 25000)
        g_Right:Set(10000, 0)
        g_Left:Set(-10000, 0)
        LoadRoom("./map0.txt")
    end

    action Update(number seconds)
    end

    action LoadRoom(text roomTextPath)
        UnloadRoom() // Remove all exisiting room objects on screen before loading new room
        roo:InitializeRoo(roomTextPath, g_ScreenSizeX, g_ScreenSizeY, g_GridX, g_GridY)

        integer i = roo:GetObjCount()
        repeat until i = 0
            Add(roo:GetObjAtIndex(i-1))
            if (roo:GetObjAtIndex(i-1):GetName() = "objCat")
                g_CatIndex = i - 1
            end
            i = i - 1
        end
	PlayerCannon testCannon
	testCannon:AddToGame(me, 100, 100)
    end

    action UnloadRoom
        g_CatIndex = -1
        repeat until roo:GetObjCount() = 0
            Remove(roo:GetObjAtIndex(0))
            roo:DeleteObjAtIndex(0)
        end
    end

    action ClickedMouse(MouseEvent event)
        integer x = event:GetX()
        integer y = event:GetY()
        output "MouseClick: " + x + "," + y

        integer startButtonIndex = roo:GetIndexOfObj("objStartButton")

        if (startButtonIndex > -1)
            number startX = roo:GetObjAtIndex(startButtonIndex):GetX()
            number startY = roo:GetObjAtIndex(startButtonIndex):GetY()
            number endX = startX + roo:GetObjAtIndex(startButtonIndex):GetWidth()
            number endY = startY + roo:GetObjAtIndex(startButtonIndex):GetHeight()

            if startX <= x
                if endX >= x
                    if startY <= y
                        if endY >= y
                            Remove(roo:GetObjAtIndex(startButtonIndex))
                            LoadRoom("./map1.txt")
                        end
                    end
                end
            end

        end
    end

    action PressedKey(KeyboardEvent event)
        if g_CatIndex > -1
            if event:keyCode = event:W
                roo:GetObjAtIndex(g_CatIndex):ApplyForceToCenter(g_Jump)
            end
            if event:keyCode = event:A
                roo:GetObjAtIndex(g_CatIndex):ApplyForceToCenter(g_Left)
                roo:GetObjAtIndex(g_CatIndex):ApplyTorque(150000)
            end
            if event:keyCode = event:D
                roo:GetObjAtIndex(g_CatIndex):ApplyForceToCenter(g_Right)
                roo:GetObjAtIndex(g_CatIndex):ApplyTorque(-150000)
            end
        end
    end

    action BeginCollision(CollisionEvent2D event)
        Drawable itemA = cast(Drawable, event:GetItemA())
        Drawable itemB = cast(Drawable, event:GetItemB())

        if itemA:GetName() = "objFish" and itemB:GetName() = "objCat"
            Remove(itemA)
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objFish"
            Remove(itemB)
        end

        if itemA:GetName() = "objExit1" and itemB:GetName() = "objCat"
            Remove(itemA)
            LoadRoom("./map2.txt")
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objExit1"
            Remove(itemB)
            LoadRoom("./map2.txt")
        end

        if itemA:GetName() = "objExit2" and itemB:GetName() = "objCat"
            Remove(itemA)
            LoadRoom("./map0.txt")
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objExit2"
            Remove(itemB)
            LoadRoom("./map0.txt")
        end

        if itemA:GetName() = "objKey" and itemB:GetName() = "objCat"
            Remove(itemA)
            integer i = roo:GetObjCount()
            repeat until i = 0
                if (roo:GetObjAtIndex(i-1):GetName() = "objLock")
                    Remove(roo:GetObjAtIndex(i-1))
                end
                i = i - 1
            end
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objKey"
            Remove(itemB)
            integer i = roo:GetObjCount()
            repeat until i = 0
                if (roo:GetObjAtIndex(i-1):GetName() = "objLock")
                    Remove(roo:GetObjAtIndex(i-1))
                end
                i = i - 1
            end
        end

    end
end