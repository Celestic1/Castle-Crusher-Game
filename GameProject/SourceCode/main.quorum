use Libraries.Game.Game
use Libraries.Game.Graphics.Drawable
use Libraries.Interface.Events.CollisionListener2D
use Libraries.Interface.Events.CollisionEvent2D
use Libraries.Interface.Events.KeyboardListener
use Libraries.Interface.Events.KeyboardEvent
use Libraries.Interface.Events.MouseListener
use Libraries.Interface.Events.MouseEvent
use Libraries.Compute.Vector2

class Main is Game, KeyboardListener, CollisionListener2D, MouseListener
    GameRoom roo

    Vector2 g_Jump
    Vector2 g_Left
    Vector2 g_Right

    action Main
        // Fixed screensize / gamename
        SetScreenSize(roo:pg_ScreenSizeX , roo:pg_ScreenSizeY)
        SetGameName("Castle Crusher")
        StartGame()
    end

    action CreateGame
        EnablePhysics2D(true)
        SetGravity2D(0, -200)
        AddKeyboardListener(me)
        AddCollisionListener(me)
        AddMouseListener(me)
        g_Jump:Set(0, 25000)
        g_Right:Set(10000, 0)
        g_Left:Set(-10000, 0)
        roo:LoadRoom("./map0.txt")
    end

    action Update(number seconds)
        // Check if there are objs that need to be removed
        repeat until roo:GetDelQueueSize() = 0
            Remove(roo:DequeueDelObj())
        end

        // Check if there are objs that need to be added
        repeat until roo:GetAddQueueSize() = 0
            Add(roo:DequeueAddObj())
        end
    end

    action ClickedMouse(MouseEvent event)
        integer x = event:GetX()
        integer y = event:GetY()
        output "MouseClick: " + x + "," + y

        integer startButtonIndex = roo:GetIndexOfObj("objStartButton")

        if (startButtonIndex > -1)
            number startX = roo:GetObjAtIndex(startButtonIndex):GetX()
            number startY = roo:GetObjAtIndex(startButtonIndex):GetY()
            number endX = startX + roo:GetObjAtIndex(startButtonIndex):GetWidth()
            number endY = startY + roo:GetObjAtIndex(startButtonIndex):GetHeight()

            if startX <= x
                if endX >= x
                    if startY <= y
                        if endY >= y
                            Remove(roo:GetObjAtIndex(startButtonIndex))
                            roo:LoadRoom("./map1.txt")
                        end
                    end
                end
            end

        end
    end

    action PressedKey(KeyboardEvent event)
        if roo:GetIndexOfObj("objCat") > -1
            if event:keyCode = event:W
                roo:GetObjAtIndex(roo:GetIndexOfObj("objCat")):ApplyForceToCenter(g_Jump)
            end
            if event:keyCode = event:A
                roo:GetObjAtIndex(roo:GetIndexOfObj("objCat")):ApplyForceToCenter(g_Left)
                roo:GetObjAtIndex(roo:GetIndexOfObj("objCat")):ApplyTorque(150000)
            end
            if event:keyCode = event:D
                roo:GetObjAtIndex(roo:GetIndexOfObj("objCat")):ApplyForceToCenter(g_Right)
                roo:GetObjAtIndex(roo:GetIndexOfObj("objCat")):ApplyTorque(-150000)
            end
        end
    end

    action BeginCollision(CollisionEvent2D event)
        Drawable itemA = cast(Drawable, event:GetItemA())
        Drawable itemB = cast(Drawable, event:GetItemB())

        if itemA:GetName() = "objFish" and itemB:GetName() = "objCat"
            Remove(itemA)
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objFish"
            Remove(itemB)
        end

        if itemA:GetName() = "objExit1" and itemB:GetName() = "objCat"
            Remove(itemA)
            roo:LoadRoom("./map2.txt")
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objExit1"
            Remove(itemB)
            roo:LoadRoom("./map2.txt")
        end

        if itemA:GetName() = "objExit2" and itemB:GetName() = "objCat"
            Remove(itemA)
            roo:LoadRoom("./map0.txt")
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objExit2"
            Remove(itemB)
            roo:LoadRoom("./map0.txt")
        end

        if itemA:GetName() = "objKey" and itemB:GetName() = "objCat"
            Remove(itemA)
            integer i = roo:GetObjCount()
            repeat until i = 0
                if (roo:GetObjAtIndex(i-1):GetName() = "objLock")
                    Remove(roo:GetObjAtIndex(i-1))
                end
                i = i - 1
            end
        elseif itemA:GetName() = "objCat" and itemB:GetName() = "objKey"
            Remove(itemB)
            integer i = roo:GetObjCount()
            repeat until i = 0
                if (roo:GetObjAtIndex(i-1):GetName() = "objLock")
                    Remove(roo:GetObjAtIndex(i-1))
                end
                i = i - 1
            end
        end

    end
end